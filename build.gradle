import java.text.SimpleDateFormat

group 'my.wf.my.wf.samlib'
version '1.1'

apply plugin: 'java'
apply plugin: "idea"

ext {
    integrationTestDir="integration-test"
    commonsIoVersion="2.4"
    h2Version="1.4.187"
    jUnitVersion="4.12"
    jacksonVersion="2.5.4"
    mockitoVersion="1.10.19"
    slf4jVersion = "1.7.12"
    //check buildscript.dependencies when change
    springBootVersion="1.2.5.RELEASE"
    swaggerVersion="2.1.2"
    springSecurityVersion="4.0.1.RELEASE"
    commonsLangVersion="3.4"
}

repositories {
    repositories {
        mavenCentral()
        mavenLocal()
    }
}

buildscript {

    repositories {
        maven{
            url "http://repo.spring.io/release"
        }
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.2.5.RELEASE")
        //classpath("io.spring.gradle:dependency-management-plugin:0.5.2.RELEASE")
    }
}


apply plugin: 'spring-boot'
//apply plugin: 'io.spring.dependency-management'




configurations{
    all*.exclude module : "jcl-over-slf4j"
    all*.exclude module : "logback-classic"
    all*.exclude module : "hamcrest-core"

    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime

}

sourceSets {

    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file("src/${integrationTestDir}/java")
        }
        resources.srcDir file("src/${integrationTestDir}/resources")
    }
}

task integrationTest(type: Test) {
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    reports.junitXml.destination = file("${project.testResultsDir}/$name")
    reports.html.destination = file("${project.reporting.baseDir}/$name")
    outputs.upToDateWhen { false }
    shouldRunAfter test
}
check.dependsOn integrationTest


tasks.withType(JavaCompile) {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

task showMeCache << {
    configurations.compile.each { println it }
}

task increaseBuildNumber << {
    Properties props = new Properties()
    File propsFile = new File('src/main/resources/version.properties')
    props.load(propsFile.newDataInputStream())
    Integer buildNumber = ((props.getProperty('build.number')) as BigDecimal);
    props.setProperty('version.number', project.version)
    props.setProperty('build.number', (++(null == buildNumber ? 0 : buildNumber)).toString())
    props.setProperty('build.time', new SimpleDateFormat("yyyy.MM.dd HH:mm:ss").format(new Date()))
    props.store(propsFile.newWriter(), null)
    props.load(propsFile.newDataInputStream())
}

dependencies {
    compile "org.slf4j:slf4j-log4j12:${slf4jVersion}"
    compile "commons-io:commons-io:${commonsIoVersion}"

    compile "org.springframework.boot:spring-boot-starter-data-jpa:${springBootVersion}"
    compile "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
    compile "org.springframework.security:spring-security-core:${springSecurityVersion}"
    compile "io.springfox:springfox-swagger-ui:${swaggerVersion}"
    compile "org.apache.commons:commons-lang3:${commonsLangVersion}"

    compile "io.springfox:springfox-swagger2:${swaggerVersion}"

    runtime "com.h2database:h2:${h2Version}"

    testCompile "junit:junit:${jUnitVersion}"
    testCompile "org.hamcrest:hamcrest-all:1.3"
    testCompile "org.mockito:mockito-all:${mockitoVersion}"
    testCompile "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"

    integrationTestCompile "junit:junit:${jUnitVersion}"
    integrationTestCompile "org.hamcrest:hamcrest-all:1.3"
    integrationTestCompile "org.mockito:mockito-all:${mockitoVersion}"
    integrationTestCompile "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"


}

jar.dependsOn increaseBuildNumber
